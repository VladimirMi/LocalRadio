apply plugin: 'com.android.application'

def secretsProperties = new Properties()

ext {
    file = new File(rootDir, "secrets.properties")
    if (file.exists()) {
        secretsProperties.load(new FileInputStream(file))
    } else {
        secretsProperties.put("partnerToken", "1234567890")
    }
}

ext.versionMajor = 1
ext.versionMinor = 2
ext.versionPatch = 0

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.vladimirmi.localradio"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'io.github.vladimirmi.localradio']
            }
        }

        buildConfigField("String", "PARTNER_TOKEN", "\"${secretsProperties['partnerToken']}\"")
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

ext {
    PLAY_SERVICES_VERSION = "16.0.0"
    ROOM_VERSION = "2.1.0-alpha01"
}

dependencies {
    //Test
    testImplementation 'junit:junit:4.12'
    //noinspection GradleCompatible
    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0"

    //Support
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.media:media:1.0.0"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.preference:preference:1.0.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha2"

    //ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:2.7.1"

    //Play services
    implementation "com.google.android.gms:play-services-location:$PLAY_SERVICES_VERSION"

    //Maps
    implementation "com.google.android.gms:play-services-maps:$PLAY_SERVICES_VERSION"
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    //Persistence
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"

    //Net
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
    implementation "com.squareup.okhttp3:okhttp:3.11.0"

    //Logging
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
    implementation "com.facebook.stetho:stetho:1.5.0"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    implementation "com.jakewharton.timber:timber:4.7.0"

    //Rx
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "io.reactivex.rxjava2:rxjava:2.1.16"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"
    implementation "com.github.tbruyelle:rxpermissions:0.10.2"

    //UI
    implementation "com.github.bumptech.glide:glide:3.8.0"
    implementation "com.github.bumptech.glide:okhttp3-integration:1.5.0@aar"
    implementation "com.jakewharton:butterknife:9.0.0-rc1"
    annotationProcessor "com.jakewharton:butterknife-compiler:9.0.0-rc1"
    implementation "com.andkulikov:transitionseverywhere:1.7.9"

    //DI
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.3"
    annotationProcessor "com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.3"

    implementation project(':playerbutton')
}
